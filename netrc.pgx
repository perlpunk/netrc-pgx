%grammar netrc
%version 0.0.1

netrc: entry* %% EOL+

entry:
| netrc-entry
| macdef-entry

macdef-entry:
| macdef-entry-nonempty
| macdef-entry-empty

macdef-entry-empty: / 'macdef' SPACE* / comment?

macdef-entry-nonempty: macdef-header macdef-body

macdef-header: / 'macdef' SPACE+ / macdef-name comment? EOL

macdef-name: / ( NS+ ) /

macdef-body: macdef-netrc macdef-commands
macdef-netrc: netrc-item-comment* %% / (: SPACE+ | EOL ) /
macdef-commands: macdef-commandline* %% EOL

macdef-commandline: / SPACE* ( ANY+ ) /

comment: / SPACE+ HASH SPACE* ( ANY* ) /

netrc-entry:
| netrc-machine-entry
| netrc-default-entry

netrc-machine-entry: / 'machine' SPACE+ / netrc-value comment? + netrc-item-comment+ % +

netrc-default-entry: / 'default' / comment? + netrc-item-comment+ % +

netrc-item-comment: netrc-item comment?

netrc-item: netrc-item-name / SPACE+ / netrc-value

netrc-item-name:
| netrc-item-name-quoted
| netrc-item-name-plain

netrc-item-name-quoted: / '"' ( 'login' | 'account' | 'password' ) '"' /

netrc-item-name-plain: / ( 'login' | 'account' | 'password' ) /

netrc-value:
| quoted-value
| plain-value

quoted-value: / '"' ( NS+ ) '"' /
plain-value: / ( NS+ ) /

end: / ( EOL+ | '' ) /
